plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.4.0' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

allprojects {
    group = 'io.github.krongdev'
    version = '0.0.1'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

ext {
    springBootVersion = '3.4.0'
    springFrameworkVersion = '6.2.0'
    hikariCpVersion = '5.1.0'
    slf4jVersion = '2.0.13'
    aspectjVersion = '1.9.22'
    jakartaAnnotationVersion = '2.1.1'
    mysqlVersion = '8.3.0'
    mockitoVersion = '5.3.1'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        withSourcesJar()
        withJavadocJar()
    }

    test {
        useJUnitPlatform()
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.assertj:assertj-core'
        testImplementation "org.mockito:mockito-core:${mockitoVersion}"
        testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
}
